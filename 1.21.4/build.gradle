plugins {
	id 'java'
	id "fabric-loom"
}

def minecraft_version = "1.21.4"
def fapi_version = "0.110.5"
group = maven_group
version = "${project.version}+${minecraft_version}"
archivesBaseName = "AxolotlClient-config"

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

repositories {
	maven {
		url = "https://moehreag.duckdns.org/maven/releases"
	}
	maven {
		url = "https://maven.terraformersmc.com/releases"
	}
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}
	mavenLocal()
}

loom {
	mods {
		"axolotlclientconfig" {
			sourceSet("main")
		}
		"axolotlclientconfig-test" {
			sourceSet("test")
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.layered {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.21.1:2024.11.17@zip")
	}

	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${fapi_version}+${minecraft_version}"

	modCompileOnlyApi("com.terraformersmc:modmenu:6.1.0-beta.3") {
		transitive = false
	}

	implementation include(project(":common"))

	include implementation("org.lwjgl:lwjgl-nanovg:3.3.3")
	include runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.3:natives-linux')
	include runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.3:natives-linux-arm64')
	include runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.3:natives-windows')
	include runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.3:natives-windows-arm64')
	include runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.3:natives-macos')
	include runtimeOnly('org.lwjgl:lwjgl-nanovg:3.3.3:natives-macos-arm64')
}

processResources {
	inputs.property 'version', version

	filesMatching('fabric.mod.json') {
		expand "version": version
	}
}

runClient {
	classpath(sourceSets.test.runtimeClasspath)
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
		}
	}

	repositories {
		maven {
			name = "owlMaven"
			def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
			url = "https://moehreag.duckdns.org/maven/" + repository
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}
